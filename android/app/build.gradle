def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
  localPropertiesFile.withReader('UTF-8') { Reader reader -> localProperties.load(reader) }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
  throw new IllegalStateException(
      "Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def clientProperties = new Properties()
def clientPropertiesFile = rootProject.file('client.properties')
if (clientPropertiesFile.exists()) {
  clientPropertiesFile.withReader('UTF-8') { Reader reader -> clientProperties.load(reader) }
}

def redirectScheme = clientProperties.getProperty('redirect.scheme')
def redirectHost = clientProperties.getProperty('redirect.host')
def redirectPathPrefix = clientProperties.getProperty('redirect.path')
if (redirectScheme == null || redirectHost == null || redirectPathPrefix == null) {
  throw new IllegalStateException(
      "Redirect URL not found. Define redirect.scheme, redirect.host, and redirect.path in " +
          "the local.properties file.")
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
  compileSdkVersion 27

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  lintOptions {
    disable 'InvalidPackage'
  }

  defaultConfig {
    applicationId "com.monzo.monzoclient"

    minSdkVersion 24
    targetSdkVersion 27

    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    manifestPlaceholders << [
        redirectScheme: redirectScheme,
        redirectHost: redirectHost,
        redirectPathPrefix: redirectPathPrefix
    ]
  }

  buildTypes {
    release {
      signingConfig signingConfigs.debug
    }
  }
}

flutter {
  source '../..'
}

ext {
  supportVersion = '27.1.0'
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  implementation "com.android.support:customtabs:$supportVersion"
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
